plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.dddjava.jig-gradle-plugin' version '2020.8.1'
}

sourceCompatibility = 11

group = 'com.github.irof'
version = System.getenv("VERSION") ?: '0.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.postgresql:postgresql:42.2.14")
    testImplementation("com.h2database:h2:1.4.200")

    testImplementation 'org.mockito:mockito-core:3.4.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'ossrh'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jig-erd'
            from components.java
            pom {
                name = 'JIG ERD'
                description = 'Entity-Relationship Diagram of JIG'
                url = 'https://github.com/irof/jig-erd'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'irof'
                        email = 'irof.ocean@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/irof/jig-erd.git'
                    developerConnection = 'scm:git:git://github.com/irof/jig-erd.git'
                    url = 'http://github.com/irof/jig-erd'
                }
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}

jig {
    modelPattern = 'jig\\.erd\\.domain\\..+'
    documentTypes = ['PackageRelationDiagram', 'BusinessRuleRelationDiagram']
}
jigReports.dependsOn(classes)
