plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'org.dddjava.jig-gradle-plugin' version '2020.8.1'
}

sourceCompatibility = 11

group = 'irof'
version = System.getenv("VERSION") ?: '0.0.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
}

publishing {
    def repositoryUrl = System.getenv("REPOSITORY_URL")
    if (repositoryUrl) {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri(repositoryUrl)
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}


bintray {
    user = project.findProperty("bintray.user")
    key = project.findProperty("bintray.key")

    configurations = ['archives']
    publications = ['gpr']
    pkg {
        repo = 'maven'
        name = 'jig-erd'
        userOrg = 'jignite'
        websiteUrl = 'https://github.com/irof/jig-erd'
        vcsUrl = 'https://github.com/irof/jig-erd.git'
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}

jig {
    modelPattern = 'jig\\.erd\\.domain\\..+'
    documentTypes = ['PackageRelationDiagram', 'BusinessRuleRelationDiagram']
}
jigReports.dependsOn(classes)
